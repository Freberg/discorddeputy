import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'org.openapi.generator' version '7.6.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.springframework.boot' version '3.3.0' apply false
	id 'org.jetbrains.kotlin.jvm' version '2.0.0' apply false
	id 'org.jetbrains.kotlin.plugin.spring' version '2.0.0' apply false
}

ext {
	springCloudVersion = '2023.0.1'
	springDocVersion = '2.5.0'
	immutablesVersion = '2.10.1'
	discord4jVersion = '3.2.6'
	joptVersion = '5.0.4'
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.freberg'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '21'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
		mavenLocal()
	}

	test {
		useJUnitPlatform()
	}

	dependencies {
		implementation 'javax.annotation:javax.annotation-api:1.3.2'
		implementation 'com.google.code.findbugs:jsr305:3.0.2'
	}
}

tasks.register('openApiGenerateClient', GenerateTask) {
	generatorName = "java"
	inputSpec = "$rootDir/api-docs.yaml".toString()
	outputDir = "$rootDir/discorddeputy-api-client".toString()
	apiPackage = "com.freberg.discorddeputy.api"
	invokerPackage = "com.freberg.discorddeputy.invoker"
	modelPackage = "com.freberg.discorddeputy.model"
	groupId = "com.freberg"
	id = "discorddeputy-api-client"
	library = "webclient"
	version = "$rootProject.version"
	generateModelTests = false
	generateApiTests = false
}

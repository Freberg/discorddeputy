import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'io.spring.dependency-management' version '1.1.3'
	id "org.openapi.generator" version "7.0.1"
}

ext {
	springVersion = "3.1.4"
	springManagmentVersion = "1.1.3"
	springCloudVersion = "2022.0.4"
	springDocVersion = "2.2.0"
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.freberg'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '21'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
		mavenLocal()
	}

	test {
		useJUnitPlatform()
	}

	dependencies {
		implementation 'javax.annotation:javax.annotation-api:1.3.2'
	}
}

task openApiGenerateClient(type: GenerateTask) {
	generatorName = "java"
	inputSpec = "$rootDir/api-docs.yaml".toString()
	outputDir = "$rootDir/discorddeputy-api-client".toString()
	apiPackage = "com.freberg.discorddeputy.api"
	invokerPackage = "com.freberg.discorddeputy.invoker"
	modelPackage = "com.freberg.discorddeputy.model"
	groupId = "com.freberg"
	id = "discorddeputy-api-client"
	library = "webclient"
	version = "$rootProject.version"
	generateModelTests = false
	generateApiTests = false
}

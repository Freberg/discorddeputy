import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.2.21' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21' apply false
}

ext {
    springCloudVersion = '2025.1.0'
    immutablesVersion = '2.11.7'
    discord4jVersion = '3.3.0'
    ktorVersion = '3.3.3'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.freberg'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}

subprojects {
    tasks.configureEach { task ->
        if (task.name == 'test' && task.hasProperty('jvmArgs')) {
            task.jvmArgs('--enable-native-access=ALL-UNNAMED')
        }
        if (task.name == 'processTestAot' || task.name == 'compileAotTestJava') {
            task.enabled = false
        }
    }
}